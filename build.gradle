plugins {
    id 'net.kyori.indra.licenser.spotless' version '2.2.0' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

version = '2.0.0-SNAPSHOT'
group = 'dev.vankka'

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'net.kyori.indra.licenser.spotless'

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    indraSpotlessLicenser {
        licenseHeaderFile(rootProject.file('buildscript/LICENSE_HEADER'))
        newLine(true)
        property('inception', '2021')
        property('year_now', String.valueOf(Calendar.getInstance().get(Calendar.YEAR)))
    }

    if (System.getenv('SONATYPE_KEY') == null && !project.hasProperty("signing.keyId")) {
        // Don't even bother with signing/publishing if neither of these are set
        return
    }
    if (project.name == 'gradle-plugin' || project.name == 'jarinjar' /* only the subprojects of this project are nedded */) {
        // Gradle plugin has it's own configuration
        return
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifactId = 'dependencydownload-' + project.name
                pom {
                    name = 'dependencydownload-' + project.name
                    packaging = 'jar'
                    description = 'A library to download dependencies during runtime'
                    url = 'https://github.com/Vankka/DependencyDownload'

                    scm {
                        connection = 'scm:git:https://github.com/Vankka/DependencyDownload.git'
                        developerConnection = 'scm:git:https://github.com/Vankka/DependencyDownload.git'
                        url = 'https://github.com/Vankka/DependencyDownload'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            id = 'Vankka'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY') != null || project.hasProperty("signing.keyId")) {
        signing {
            sign publishing.publications.maven

            def key = System.getenv('SIGNING_KEY')
            if (key != null) {
                useInMemoryPgpKeys(key, System.getenv('SIGNING_KEY_PASS'))
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri('https://s01.oss.sonatype.org/service/local/'))
            snapshotRepositoryUrl.set(uri('https://s01.oss.sonatype.org/content/repositories/snapshots/'))
            packageGroup = rootProject.group
        }
    }
}